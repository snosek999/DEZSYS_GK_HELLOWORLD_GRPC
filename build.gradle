plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.5'
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.70.0'
def protobufVersion = '3.25.8'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // Java 9+ annotation workaround
    if (JavaVersion.current().isJava9Compatible()) {
        compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
        annotationProcessor "jakarta.annotation:jakarta.annotation-api:2.1.1"
    }

    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

tasks.register('runServer', JavaExec) {
    group = 'application'
    description = 'Runs the gRPC server'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'HelloWorldServer'
}

tasks.register('runClient', JavaExec) {
    group = 'application'
    description = 'Runs the gRPC client'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'HelloWorldClient'
     args 'Max'
     args 'Mustermann'
}
